; Vertex Shader for ImGui Citro3d Backend
; Copyright (c) 2020 - 2023 Michael Theall, NPI-D7
; Commentarys are a bit modyfied and proj is now projection
; Original Shader: https://github.com/mtheall/ftpd/blob/master/source/3ds/vshader.v.pica

; Projection Matrix
.fvec projection[4]

; Constants
.constf constants(1.0, 0.0, 0.00392156862745, 0.0)

; Outputs
.out outPos   position
.out outUv    texcoord0
.out outColor color

; Inputs
.alias inPos   v0
.alias inUv    v1
.alias inColor v2

.proc main
        ; inPosition .z = 0.0 | .w = 1.0
        mov r0.xy, inPos.xy
	mov r0.zw, constants.yx

        ; outPosition = projection*inPosition
        dp4 outPos.x, projection[0], r0
	dp4 outPos.y, projection[1], r0
	dp4 outPos.z, projection[2], r0
	dp4 outPos.w, projection[3], r0

        ; outTexcoord = inTexcoord
        mov outUv, inUv

        ; Normalize Color
        mul r1, constants.zzzz, inColor

        ; outColor = inColor
        mov outColor, r1

        ; End
        end
.end